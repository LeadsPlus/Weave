<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<SimpleVisTool xmlns="weave.visualization.tools.*" 
	     xmlns:mx="http://www.adobe.com/2006/mxml"
	     xmlns:ui="weave.ui.*"
		 xmlns:userControls="weave.ui.userControls.*"
		 xmlns:settings="weave.ui.settings.*" layout="absolute">
	<mx:Script>
		<![CDATA[
			import mx.utils.StringUtil;
			
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.core.ILinkableObject;
			import weave.api.data.AttributeColumnMetadata;
			import weave.api.data.IAttributeColumn;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.linkSessionState;
			import weave.api.registerLinkableChild;
			import weave.api.ui.IVisTool;
			import weave.core.LinkableBoolean;
			import weave.data.AttributeColumns.AlwaysDefinedColumn;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.EquationColumn;
			import weave.primitives.Bounds2D;
			import weave.ui.AttributeSelectorPanel;
			import weave.ui.ColumnListComponent;
			import weave.utils.BitmapText;
			import weave.utils.ColumnUtils;
			import weave.utils.LinkableTextFormat;
			import weave.utils.ProbeTextUtils;
			import weave.visualization.layers.LayerSettings;
			import weave.visualization.plotters.ParallelCoordinatesPlotter;
			import weave.visualization.plotters.TextGlyphPlotter;
			
			WeaveAPI.registerImplementation(IVisTool, LineChartTool, "Line Chart");
			
			[Bindable]
			private var _plotter:ParallelCoordinatesPlotter = null;
			override protected function constructor():void
			{
				super.constructor();
				
				visualization.plotManager.marginBottom.value = "80";
				visualization.plotManager.marginTop.value    = "10";
				visualization.plotManager.marginLeft.value   = "80";
				visualization.plotManager.marginRight.value  = "80";
				
				// lock dynamic objects into place
				_plotter = initializePlotters(ParallelCoordinatesPlotter, true);
				
				// default axis values
				yAxisPlotter.tickCountRequested.value = 5;
				xAxisPlotter.forceTickCount.value = true;

				visualization.plotManager.includeNonSelectableLayersInAutoZoom.lock();
				visualization.enableZoomAndPan.value = false;

				//This variable represents whether or not the tool is a 
				//lineChart(false) or parallelCoords(true)
				_plotter.normalize.value = false;
				_plotter.normalize.lock();
				
				addLabelLayer();
				
				linkSessionState(labelPlotter.zoomToSubset, _plotter.zoomToSubset);

				getCallbackCollection(visualization.plotManager.zoomBounds).addGroupedCallback(this, updateAxes);
			
				// BEGIN TEMPORARY SOLUTION
				visualization.plotManager.marginLeftNumber.addGroupedCallback(this, updateAxes);
				visualization.plotManager.marginBottomNumber.addGroupedCallback(this, updateAxes);
				visualization.plotManager.marginTopNumber.addGroupedCallback(this, updateAxes);
				visualization.plotManager.marginRightNumber.addGroupedCallback(this, updateAxes);

				getCallbackCollection(_plotter).addGroupedCallback(this, updateAxes, true);
				// END TEMPORARY SOLUTION
				var self:* = this;
				visualization.bottomMarginClickCallbacks.addImmediateCallback(this, function():void { AttributeSelectorPanel.openToolSelector(self, _plotter.columns); });
				
				var count:int = getDefaultColumnsOfMostCommonKeyType().length;
				var columnsToInit:Array = [];
				for (var i:int = 0; i < count; i++)
					columnsToInit.push(_plotter.columns.requestObject(null, DynamicColumn, false));
				if (columnsToInit.length > 0)
					initColumnDefaults.apply(null, columnsToInit);
				
				_plotter.columns.addGroupedCallback(this, updateProbeColumns, true);
				_plotter.xData.addGroupedCallback(this, updateFilterText);
				_plotter.xValues.addGroupedCallback(this, updateFilterText, true);
				
				linkSessionState(visualization.plotManager.marginRightNumber, labelPlotter.maxWidth);
			}

			public const includeToolAttributesInProbe:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(true), updateProbeColumns);
			
			private function updateProbeColumns():void
			{
				if (includeToolAttributesInProbe.value)
				{
					visualization.additionalProbeColumns = _plotter.columns.getObjects(IAttributeColumn);
					visualization.additionalProbeColumns.unshift(labelPlotter.text, colorColumn);
				}
				else
					visualization.additionalProbeColumns = null;
			}
			
			/**
			 * This function generates a label equation using the current Label column and the plotter's filterColumn
			 * and makes it the new Label column
			 */
			private function updateLabelColumn():void
			{
				if(!_plotter.enableGroupBy.value) 
					return;
				if(!labelPlotter.text.getInternalColumn())
					return;
				if(ColumnUtils.getKeyType(labelPlotter.text) != ColumnUtils.getKeyType(_plotter.xData))
					return;
				if(labelPlotter.text.getInternalColumn() is EquationColumn)
					return;
				
				var labelColumn:DynamicColumn = new DynamicColumn();
				labelColumn.requestLocalObjectCopy(labelPlotter.text);
				
				var labelEquation:EquationColumn = labelPlotter.text.requestLocalObject(EquationColumn, false) as EquationColumn;
				labelEquation.delayCallbacks();
				labelEquation.variables.requestObjectCopy("labelColumn",labelColumn);
				labelEquation.variables.requestObjectCopy("keyColumn", _plotter.groupBy);
				labelEquation.setMetadata(AttributeColumnMetadata.TITLE, ColumnUtils.getTitle(labelColumn) + "Equation");
				labelEquation.equation.value = 'key = getAssociatedKeys(keyColumn, getKey())[0], getValueFromKey(labelColumn, key, String)';
				labelEquation.resumeCallbacks();
				/* 
				if(!_plotter.lineStyle.color.internalDynamicColumn)
					return;
				
				if(_plotter.lineStyle.color.internalDynamicColumn is EquationColumn)
					return;
				
				var color:DynamicColumn = new DynamicColumn();
				color.requestGlobalObject(Weave.DEFAULT_COLOR_COLUMN, ColorColumn, false);
				
				var equation:EquationColumn = colorColumn.requestLocalObject(EquationColumn, false) as EquationColumn;
				equation.variables.copyObject("colorColumn", color);
				equation.variables.copyObject("key", _plotter.keyColumn);
				equation.columnTitle = ColumnUtils.getTitle(color)+'Equation';
				equation.equation.value = 'key = getKeysFromValue(get("key"), getKey(), QKey)[0], getValueFromKey(get("colorColumn"), key, Number)';
				 */
			}
			
			private function updateFilterText():void
			{
				xValuesInput.text = WeaveAPI.CSVParser.createCSV([_plotter.getXValues()]);
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				xAxisPlotter.setLabelFunction(getColumnTitle);
				xAxisPlotter.axisLabelRelativeAngle.value = -45;
				xAxisPlotter.labelHorizontalAlign.value = BitmapText.HORIZONTAL_ALIGN_RIGHT;
				xAxisPlotter.labelVerticalAlign.value = BitmapText.VERTICAL_ALIGN_TOP;
				xAxisPlotter.labelDistanceIsVertical.value = false;
				
				yAxisPlotter.setLabelFunction(yAxisLabelFunction);
				
				visualization.plotManager.includeNonSelectableLayersInAutoZoom.value = false;
				
				(labelComponent as DynamicColumnComponent).dynamicObject = labelPlotter.text;
				
				linkBindableProperty(labelPlotter.size.defaultValue,labelSizeSlider,"value");
				linkBindableProperty(labelPlotter.color.defaultValue, colorPicker, "selectedColor");

				linkBindableProperty(_plotter.alphaColumn.defaultValue, 		alphaSlider,		"value");
				linkBindableProperty(_plotter.shapeSize,  					shapeSizeSlider, 	"value");
				linkBindableProperty(_plotter.lineStyle.weight.defaultValue, thicknessSlider, 	"value");
				linkBindableProperty(_plotter.shapeToDraw, shapeCombo, "selectedItem");
				linkBindableProperty(_plotter.shapeBorderThickness, shapeBorderSizeSlider, "value");
				linkBindableProperty(_plotter.shapeBorderColor, shapeBorderColorPicker, "selectedColor");
				_plotter.curveType.addGroupedCallback(this, handleCurveType, true);
				function handleCurveType():void
				{
					curveCheckBox.selected = _plotter.curveType.value == ParallelCoordinatesPlotter.CURVE_DOUBLE;
				}
			}
			
			override public function getSelectableAttributeNames():Array
			{
				return ["Color","Label","Columns"];
			}
			override public function getSelectableAttributes():Array
			{
				return [colorColumn,labelPlotter.text,_plotter.columns];
			}
			
			public function get colorColumn():DynamicColumn
			{
				return Weave.defaultColorDataColumn.internalDynamicColumn;
			}
			
			private function getColumnTitle( index:Number ):String
			{
				try {
					var columns:Array = _plotter.columns.getObjects();
					return ColumnUtils.getTitle(columns[index]);
				} catch(e:Error) {}
				return "";
			}
			
			private static const tempBounds:Bounds2D = new Bounds2D(); // reusable temporary object
			
			private function yAxisLabelFunction(value:Number):String
			{
				var _columns:Array = _plotter.columns.getObjects();
				if (_columns.length > 0)
					return ColumnUtils.deriveStringFromNumber(_columns[0], value); // always use the first column to format the axis labels
				return null;
			}
			
			private function updateAxes():void
			{
				handlePanelTitleChange();
				
				var columns:Array = _plotter.columns.getObjects();
				
				xAxisPlotter.tickCountRequested.value = Math.max(2, columns.length);
				
				// get bounds for y axis
				visualization.plotManager.zoomBounds.getDataBounds(tempBounds);
				tempBounds.xMax = tempBounds.xMin;
				
				yAxisPlotter.axisLineMinValue.value = tempBounds.yMin;
				yAxisPlotter.axisLineMaxValue.value = tempBounds.yMax;
			
				yAxisPlotter.axisLineDataBounds.copyFrom(tempBounds);
				
				var xAxisName:String = '';
				var yAxisName:String = '';
				if (_plotter.enableGroupBy.value)
				{
					xAxisName = ColumnUtils.getTitle(_plotter.xData);
					yAxisName = ColumnUtils.getTitle(_plotter.yData);
				}
				xAxisPlotter.setSideAxisName(
					xAxisName,
					0,
					0, visualization.plotManager.marginBottomNumber.value - 3,
					BitmapText.VERTICAL_ALIGN_BOTTOM
				);
				yAxisPlotter.setSideAxisName(
					yAxisName,
					-90,
					-visualization.plotManager.marginLeftNumber.value, 0,
					BitmapText.VERTICAL_ALIGN_TOP
				);

				xAxisPlotter.updateLabels();
				yAxisPlotter.updateLabels();			
			}
			
			override protected function get defaultPanelTitle():String
			{
				var columns:Array = _plotter.columns.getObjects();
				if (columns.length == 0)
					return 'Line Chart';
				
				if (_plotter.enableGroupBy.value)
					return StringUtil.substitute(
						"Line Chart of {0} by {1}",
						ColumnUtils.getTitle(_plotter.yData),
						ColumnUtils.getTitle(_plotter.xData)
					);
				
				var newTitle:String = "Line Chart of ";
				for (var i:int = 0; i < columns.length; i++)
				{
					if (i > 0)
						newTitle += ', ';
					newTitle += ColumnUtils.getTitle(columns[i]);
				}
				return newTitle;
			}
			
			private function handleCurveCheckBox():void
			{
				if (curveCheckBox.selected)
					_plotter.curveType.value = ParallelCoordinatesPlotter.CURVE_DOUBLE;
				else
					_plotter.curveType.value = ParallelCoordinatesPlotter.CURVE_NONE;
			}
			
			private function alphaSliderFormatFunction(value:Number):String {
            	//return int(100 - (value * 100)) + "% Transparent";
            	return int((value * 100)) + "% Opaque" + "\n" + int(100 - (value * 100)) + "% Transparent";
            }
			
			private var labelPlotter:TextGlyphPlotter = null ;
			private function addLabelLayer():void
			{
				var name:String = "label";
				var labelPlotter:TextGlyphPlotter = visualization.plotManager.plotters.requestObject(name, TextGlyphPlotter, true);
				var settings:LayerSettings = visualization.plotManager.getLayerSettings(name);
				settings.selectable.value = false;
				settings.selectable.lock();
				
				_plotter.columns.addImmediateCallback(this, updateColumns, true);

				labelPlotter.xScreenOffset.value = 5;
				labelPlotter.hAlign.defaultValue.value = BitmapText.HORIZONTAL_ALIGN_LEFT;
				labelPlotter.hideOverlappingText.value = true;
				linkSessionState(labelPlotter.dataY, labelPlotter.sortColumn);
				labelPlotter.setBaseKeySource(_plotter.keySet);
				getCallbackCollection(LinkableTextFormat.defaultTextFormat).addGroupedCallback(this, updateLabel, true);
			}
			private function updateLabel():void
			{
				labelPlotter.setDefaultTextFormat(LinkableTextFormat.defaultTextFormat);
			}
			
			private function updateColumns():void
			{
				var _columns:Array = _plotter.columns.getObjects();
				var x:AlwaysDefinedColumn = labelPlotter.dataX.requestLocalObject(AlwaysDefinedColumn, false);
				x.defaultValue.value = _columns.length;
				
				if (_columns.length == 0)
				{
					labelPlotter.dataY.removeObject();
					return;
				}
				var lastColumn:IAttributeColumn;
				var repeat:Boolean = false;
				do
				{
					repeat = false;
					if( _columns.length == 0 )
						break;
					x.defaultValue.value = (x.defaultValue.value as Number) - 1;
					lastColumn= _columns.pop();
					labelPlotter.dataY.requestLocalObjectCopy(lastColumn as ILinkableObject);
					if( labelPlotter.dataY.keys[0] != undefined )
						repeat = isNaN(lastColumn.getValueFromKey(labelPlotter.dataY.keys[0], Number));
				} while (labelPlotter.dataY.keys[0] == undefined || repeat );

				if (labelPlotter.text.getInternalColumn() == null)
				{
					var headers:Array = ProbeTextUtils.probeHeaderColumns.getObjects();
					for (var i:int = 0; i < headers.length; i++)
					{
						if (ColumnUtils.getKeyType(headers[i]) == ColumnUtils.getKeyType(lastColumn))
						{
							labelPlotter.text.requestLocalObjectCopy(headers[i]);
							break;
						}
					}
				}
			}
			
		]]>
	</mx:Script>
	<mx:Number id="controlsFontSize">10</mx:Number>
	<ui:ControlPanel>
		<mx:VBox id="attributesPanel" label="Data">
			<ui:DynamicColumnComponent visTool="{this}" label="Color" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = colorColumn" showLinkControl="false"/>
			<ui:DynamicColumnComponent visTool="{this}" label="Label" id="labelComponent"/>
			<ui:ColumnListComponent visTool="{this}" label="Attributes along X axis" creationComplete="(event.target as ColumnListComponent).hashMap = _plotter.columns"/>
			<mx:HBox>
				<mx:CheckBox id="probeColumnsCheckBox" label="Include tool attributes in probe tooltip"
							 toolTip="Checking this box makes all of the tool's attributes visible in this tool's probe tooltip"
							 creationComplete="linkBindableProperty(includeToolAttributesInProbe,event.target,'selected')"/>
			</mx:HBox>
		</mx:VBox>
		<mx:VBox id="VizControlsPanel" label="Appearance">
			<mx:HBox verticalAlign="middle">
				<mx:Label text="Shape:" color="0x000000" fontSize="{controlsFontSize}" 
						  toolTip="This controls what shape is used to indicate the data points on the axes."/>
				<ui:CustomComboBox id="shapeCombo" dataProvider="{ParallelCoordinatesPlotter.shapesAvailable}"/>
				<mx:HBox enabled="{shapeCombo.selectedItem != ParallelCoordinatesPlotter.NO_SHAPE}" verticalAlign="middle">
					<mx:Label text="Size:"
							  color="0x000000" fontSize="{controlsFontSize}" 
							  toolTip="This controls the size of the shapes indicating the data points on the axes."/>
					<mx:HSlider id="shapeSizeSlider"
								width="100%" 
								snapInterval="1"
								tickInterval="5"
								labels="{['0','5','10','15','20']}"
								minimum="0" maximum="20" 
								liveDragging="true"/>
				</mx:HBox>
			</mx:HBox>
			
			<mx:HBox verticalAlign="middle">
				<mx:HBox
						enabled="{!(shapeCombo.selectedItem == ParallelCoordinatesPlotter.NO_SHAPE || 
						shapeCombo.selectedItem == ParallelCoordinatesPlotter.EMPTY_CIRCLE ||
						shapeCombo.selectedItem == ParallelCoordinatesPlotter.EMPTY_SQUARE ||
						shapeBorderSizeSlider.value == 0) }">
					<mx:Label text="Shape border color:" color="0x000000" fontSize="{controlsFontSize}"/>
					<mx:ColorPicker id="shapeBorderColorPicker"
									toolTip="This controls color of the border around the shape at each point."/>
				</mx:HBox>
				
				<mx:HBox width="100%" verticalAlign="middle"
						enabled="{shapeCombo.selectedItem != ParallelCoordinatesPlotter.NO_SHAPE}">
					<mx:Label text="Thickness:" color="0x000000" fontSize="{controlsFontSize}" 
							  toolTip="This controls the border size of the shapes indicating the data points on the axes."/>
					<mx:HSlider id="shapeBorderSizeSlider" 
								width="100%" 
								snapInterval="1"
								tickInterval="1" 
								labels="{['0', '1', '2', '3', '4', '5']}"
								minimum="0" maximum="5"
								liveDragging="true"/>
				</mx:HBox>
			</mx:HBox>
			
			<mx:HBox verticalAlign="middle">
				<mx:Label text="Line Opacity:" toolTip="Use this control to see how transparent lines are.  Moving left increases transparency (decreased opacity), moving to the right decreases transparency (increased opacity) up to fully opaque (no transparency)."/>
				<mx:HSlider id="alphaSlider" 
							width="100%" 
							snapInterval="0.1"
							tickInterval="0.5" labels="{['0%', '50%', '100%']}"
							dataTipFormatFunction="alphaSliderFormatFunction"
							minimum="0.0" maximum="1.0" 
							liveDragging="true"/>
				<mx:Label text="Thickness:"/>
				<mx:HSlider id="thicknessSlider" minimum="1" maximum="4" labels="{['1','2','3','4']}" snapInterval="1" liveDragging="true" tickInterval="1"/>
				
			</mx:HBox>
			<mx:CheckBox id="curveCheckBox" label="Curved lines"
						 change="_plotter.curveType.value = curveCheckBox.selected ? ParallelCoordinatesPlotter.CURVE_DOUBLE : ParallelCoordinatesPlotter.CURVE_NONE"/>
			
			<mx:HBox verticalAlign="middle">
				<mx:Label text="Label Text Size: "/>
				<mx:HSlider id="labelSizeSlider"
							width="100%"
							alpha="1.0"
							snapInterval="1"
							tickInterval="5" labels="{['5','10','15','20']}"
							minimum="5" maximum="20" 
							liveDragging="true"/>
				<mx:Label text="Color:"/>
				<mx:ColorPicker id="colorPicker" width="22" height="22"/>
			</mx:HBox>

			<mx:CheckBox id="zoomToSubsetCheckBox" label="Zoom to Subset" 
						 toolTip="Enables automatic zooming to the subset of data currently displayed"
						 creationComplete="linkBindableProperty(_plotter.zoomToSubset, event.target, 'selected')"/>
		</mx:VBox>
		<mx:VBox label="Grouping">
			<mx:CheckBox id="enableGroupByCheckBox" creationComplete="linkBindableProperty(_plotter.enableGroupBy, event.target, 'selected')"
						 label="Group records by column values"
						 toolTip="Enable/disable grouping data by category value"/>
			<mx:VBox enabled="{enableGroupByCheckBox.selected}">
				
				<mx:VBox paddingLeft="20" borderStyle="solid">
					<ui:DynamicColumnComponent visTool="{this}" label="X Column" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = _plotter.xData"
											   toolTip="Attribute to appear on the x axis. All columns must have the same keyType."/>
					<ui:DynamicColumnComponent visTool="{this}" label="Y Column" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = _plotter.yData"
											   toolTip="Attribute to appear on the y axis. All columns must have the same keyType."/>
					<ui:DynamicColumnComponent visTool="{this}" label="Group by" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = _plotter.groupBy"
											   toolTip="Specify column to get keys from. All columns must have the same keyType."/>
					<ui:DynamicColumnComponent visTool="{this}" label="Label" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = labelPlotter.text"
											   toolTip="Attribute used for record label text. All columns must have the same keyType."/>
				</mx:VBox>
				<mx:Button label="Generate label Equation" toolTip="Generates label equation from current Label column if its keyType matches the group-by keyType"
						   click="updateLabelColumn()"/>
				<mx:HBox width="100%">
					<mx:Label text="Customize X values:"/>
					<mx:TextInput id="xValuesInput" toolTip="These values will appear on the x axis" width="100%"/>
					<mx:Button label="Apply" click="_plotter.xValues.value = xValuesInput.text;"/>
					<mx:Button label="Reset" click="_plotter.xValues.value = ''; updateFilterText();"/>
				</mx:HBox>
			</mx:VBox>
		</mx:VBox>
	</ui:ControlPanel>
</SimpleVisTool>
