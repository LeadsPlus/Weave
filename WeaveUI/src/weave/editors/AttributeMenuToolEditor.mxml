<?xml version="1.0" encoding="utf-8"?>
<!--
/*
Weave (Web-based Analysis and Visualization Environment)
Copyright (C) 2008-2011 University of Massachusetts Lowell

This file is a part of Weave.

Weave is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License, Version 3,
as published by the Free Software Foundation.

Weave is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<mx:VBox xmlns="weave.ui.*" xmlns:mx="http://www.adobe.com/2006/mxml" 
			xmlns:tools="weave.visualization.tools.*" xmlns:editors="weave.editors.*" implements="weave.api.ui.ILinkableObjectEditor">
	

		<mx:VBox width="100%" label="Settings">
			<mx:HBox>
				<mx:VBox>
					<Indent label="Visualization tool:" indent="140">
						<CustomComboBox prompt="Select a tool..." id="targetToolCombo" enabled="{targetToolCombo.dataProvider.length > 0}"
										valueCommit="handleComboChange(targetToolCombo, _target.targetTool);" />
						<HelpComponent>Select the visualization tool that you want the attribute menu to be linked to.</HelpComponent>
					</Indent>
					<Indent label="Visualization attribute:" indent="140" toolTip="The selected attribute will be modified by the combo box in this menu tool.">
						<CustomComboBox prompt="Select an attribute..." id="targetAttributeCombo" enabled="{targetAttributeCombo.dataProvider.length > 0}"
										valueCommit="handleComboChange(targetAttributeCombo, _target.targetAttribute);" />
						<HelpComponent>Select the visualization attribute that you want the menu to be linked to.</HelpComponent>
					</Indent>
					<Indent label="Menu layout:" indent="140">
						<CustomComboBox id="layoutCombo" prompt="Select a layout..." labelFunction="{layoutLabelFunction}"
										dataProvider="{layouts}"/>
						<HelpComponent>Select the layout for the attribute menu.</HelpComponent>
					</Indent>
				</mx:VBox>
			</mx:HBox>
			<ColumnListComponent id="choicesComponent" label="Attributes to appear in the menu" />
			<Indent label="Label function">
				<mx:TextInput width="100%" creationComplete="linkBindableProperty(_target.labelFunction, event.target, 'text');"/>
				<mx:Button label="Reset" click="_target.labelFunction.value = DEFAULT_LABEL_FUNCTION;"/>
			</Indent>
		</mx:VBox>
		<editors:WindowSettingsEditor target="{this}"/>
	<mx:Script>
		<![CDATA[
			import mx.controls.ComboBox;
			import mx.utils.StringUtil;
			
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.copySessionState;
			import weave.api.core.ILinkableHashMap;
			import weave.api.core.ILinkableObject;
			import weave.api.data.IAttributeColumn;
			import weave.api.linkBindableProperty;
			import weave.api.newLinkableChild;
			import weave.api.registerLinkableChild;
			import weave.api.reportError;
			import weave.api.ui.IVisTool;
			import weave.core.LinkableFunction;
			import weave.core.LinkableHashMap;
			import weave.core.LinkableString;
			import weave.core.UIUtils;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.ui.AttributeMenuTool;
			import weave.ui.AttributeSelectorPanel;
			import weave.utils.ColumnUtils;
			import weave.utils.ProbeTextUtils;
			import weave.visualization.tools.SimpleVisTool;


			private static const DEFAULT_LABEL_FUNCTION:String = 'column.getMetadata("title")';
			

			// used to label the choices
			private function choiceLabelFunction(column:IAttributeColumn):String
			{
				var result:String = ColumnUtils.getTitle(column);
				if (_target.labelFunction.value)
				{
					try
					{
						result = _target.labelFunction.apply(this, [column]) || '';
					}
					catch (e:Error)
					{
						result = '';
					}
				}
				return result;
			}
			
			private static const layouts:Array = ["ComboBox", "List", "VSlider", "HSlider"];
			private static const layoutLabels:Array = ["Combo Box", "List", "Vertical Slider", "Horizontal Slider"];
			private static function verifyLayoutMode(value:String):Boolean { return layouts.indexOf(value) >= 0; }

			private function layoutLabelFunction(inputString:String):String
			{
				return layoutLabels[layouts.indexOf(inputString)];
			}
			
			// called after children have been created
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				
				// Since we're adding these callbacks after the children have been created,
				// we don't have to worry about ui components being null inside the callbacks.
				Weave.root.childListCallbacks.addGroupedCallback(this, handleGlobalListChange, true);
				_target.targetTool.addImmediateCallback(this, handleTargetToolChange, true);
				_target.targetAttribute.addImmediateCallback(this, handleTargetAttributeChange, true);
				_target.choices.addImmediateCallback(this, handleChoicesChange, true);
				
				choicesComponent.hashMap = _target.choices;
				
				linkBindableProperty(_target.layoutMode, layoutCombo, 'selectedItem');
				_target.layoutMode.addImmediateCallback(this, handleLayoutMode);
				
				callLater(showControlPanelIfNothingSelected);
			}
			
			private function handleLayoutMode():void
			{
				_target.layoutViewStack.selectedIndex = layouts.indexOf(_target.layoutMode.value);
			}
			
			private function showControlPanelIfNothingSelected():void
			{
				// when the tool is created and nothing is selected, show the control panel
				if (!_target.targetTool.value && !_target.targetAttribute.value && !WeaveAPI.StageUtils.mouseButtonDown)
					_target.toggleControlPanel();
			}
			
			// this returns the tool object corresponding to the selected item in the toolCombo
			public function getTargetTool():SimpleVisTool
			{
				return Weave.root.getObject(_target.targetTool.value) as SimpleVisTool;
			}
			
			// called whenever a global object gets added/removed/reordered
			private function handleGlobalListChange():void
			{
				// update the list of tools
				targetToolCombo.dataProvider = Weave.root.getNames(SimpleVisTool);
				// after we set the data provider, we have to make sure the correct item is selected
				handleTargetToolChange();
			}
			
			// called when the target tool changes
			private function handleTargetToolChange():void
			{
				// make sure the correct item is selected
				targetToolCombo.selectedItem = _target.targetTool.value;
				
				var tool:SimpleVisTool = getTargetTool();
				// update the list of available attributes
				if (tool)
				{
					var attrNames:Array = tool.getSelectableAttributeNames();
					targetAttributeCombo.dataProvider = attrNames;
				}
				else
				{
					targetAttributeCombo.dataProvider = [];
				}
				
				// after we set the data provider, we have to make sure the correct item is selected
				handleTargetAttributeChange();
			}
			
			// called when the target attr changes
			private function handleTargetAttributeChange():void
			{
				var tool:SimpleVisTool = getTargetTool();
				// only enable the choiceCombo if the target attribute exists in the target tool
				if (tool)
				{
					var attrNames:Array = tool.getSelectableAttributeNames();
					var index:int = attrNames.indexOf(_target.targetAttribute.value);
					if (index >= 0)
					{
						// found the attribute name in the list, now check if it's a compatible object
						var attrObjs:Array = tool.getSelectableAttributes();
						var obj:Object = attrObjs[index];
						var compatible:Boolean = obj is DynamicColumn || obj is ILinkableHashMap;
						_target.layoutViewStack.enabled = compatible;
					}
					else
					{
						_target.layoutViewStack.enabled = false;
					}
				}
				else
				{
					_target.layoutViewStack.enabled = false;
				}
				
				// make sure the correct item is selected
				targetAttributeCombo.selectedItem = _target.targetAttribute.value;
				// clear the choice
				_target.choiceCombo.selectedIndex = -1;
				_target.choiceList.selectedIndex = -1;
			}
			
			// called when the admin alters the choices through the control panel
			private function handleChoicesChange():void
			{
				// update the list of column objects available through the main window combo box
				_target.choiceCombo.dataProvider = _target.choices.getObjects();
				_target.choiceList.dataProvider = _target.choices.getObjects();
				
				// get a list of labels
				var columns:Array = _target.choices.getObjects();
				var names:Array = [];
				for (var i:uint = 0; i < columns.length; i++)
					names[i] = choiceLabelFunction(columns[i]);
				
				// special case for VSlider -- reverse order
				_target.choiceVSlider.labels = names.concat().reverse();
				_target.choiceHSlider.labels = names;
				_target.choiceVSlider.maximum = names.length - 1;
				_target.choiceHSlider.maximum = names.length - 1;
			}
			
			// called when combo box selection changes, makes sure it has focus before setting target LinkableString
			private function handleComboChange(combo:ComboBox, target:LinkableString):void
			{
				if (UIUtils.hasFocus(combo))
					target.value = combo.selectedItem as String;
			}
			
			// called when the user clicks on the combo in the main AttributeMenuTool window
			private function handleAttrSelected(selectedIndex:int):void
			{
				var tool:SimpleVisTool = getTargetTool();
				// do nothing if there is no tool or the component doesn't have focus
				if (!tool)
					return;
				
				var attrNames:Array = tool.getSelectableAttributeNames();
				var attrObjs:Array = tool.getSelectableAttributes();
				// find the index of the tool attribute selected in the control panel
				var index:int = attrNames.indexOf(_target.targetAttribute.value);
				if (index < 0)
				{
					// we didn't find the "SelectableAttributeName"
					reportError(StringUtil.substitute('"{0}" does not have attribute "{1}"', _target.targetTool.value, _target.targetAttribute.value));
					return;
				}
				
				// using the index, get the corresponding "SelectableAttribute" from the tool
				var dynamicColumn:DynamicColumn = attrObjs[index] as DynamicColumn;
				var hashMap:ILinkableHashMap = attrObjs[index] as ILinkableHashMap;
				
				if (hashMap)
				{
					ColumnUtils.forceFirstColumnDynamic(hashMap);
					dynamicColumn = hashMap.getObjects(IAttributeColumn)[0];
				}
				
				// use the same hack used by the AttributeSelectorPanel to find a nested dynamic column.
				dynamicColumn = AttributeSelectorPanel.hack_findInternalDynamicColumn(dynamicColumn);
				
				if (dynamicColumn)
				{
					// get the column object the user clicked on and set the tool attribute
					var availableChoices:Array = _target.choices.getObjects(IAttributeColumn);
					var selectedAttr:IAttributeColumn = availableChoices[selectedIndex] as IAttributeColumn;
					if (selectedAttr)
					{
						dynamicColumn.requestLocalObjectCopy(selectedAttr);
						_target.selectedAttribute.value = _target.choices.getName(selectedAttr);
					}
				}
				else
				{
					reportError('This option is not supported for the attribute "' + _target.targetAttribute.value + '"');
				}
			}
			
			private function handleSelectedAttributeChange():void
			{
				var names:Array = _target.choices.getNames();
				var selectedIndex:int = names.indexOf(_target.selectedAttribute.value);
				_target.choiceCombo.selectedIndex = selectedIndex;
				_target.choiceList.selectedIndex = selectedIndex;
				if (selectedIndex >= 0)
				{
					_target.choiceHSlider.value = selectedIndex;
					_target.choiceVSlider.value = names.length - 1 - selectedIndex;
					handleAttrSelected(selectedIndex);
				}
			}
			
			private var _target:AttributeMenuTool;
			
			public function setTarget(object:ILinkableObject):void
			{
				_target = object as AttributeMenuTool;
			}
			public function applyChanges():void
			{
				
			}
		]]>
	</mx:Script>
</mx:VBox>
	
