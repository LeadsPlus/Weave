<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<ui:DraggablePanel xmlns="weave.example.*" xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:ui="weave.ui.*"
				   implements="weave.api.ui.IVisTool" enterFrame="report()">
	<mx:VBox>
		<mx:HBox>
			<mx:Label text="This is an example panel"/>
			<ui:HelpComponent>Example help text</ui:HelpComponent>
			<mx:Button label="test1" click="redraw1()"/>
			<mx:Button label="test2" click="redraw2()"/>
			<mx:Button label="test3" click="redraw3()"/>
			<mx:Button label="test4" click="redraw4()"/>
			<mx:NumericStepper id="numIterations" minimum="1000" maximum="10000" value="5000" stepSize="1000" toolTip="Number of circles to render"/>
		</mx:HBox>
	
		<mx:Canvas width="100%" height="100%" id="canvas" mouseChildren="false"/>
	</mx:VBox>
	<mx:Script>
		<![CDATA[
			import weave.api.WeaveAPI;
			import weave.api.ui.IVisTool;
			WeaveAPI.registerImplementation(IVisTool, Example);
			
			private var bitmap:Bitmap = new Bitmap(new BitmapData(500, 500, true, 0x00000000));
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				canvas.rawChildren.addChild(bitmap);
			}
			
			private var t:int = -1; // start time
			private var wait:Boolean = false; // wait 1 frame?
			
			private function redraw1():void
			{
				wait = true;
				t = getTimer();
				// 10,000 circles in same position
				// 16.5 seconds
				canvas.graphics.clear();
				bitmap.visible = false;
				for (var i:int = 0; i < numIterations.value; i++) {
					canvas.graphics.lineStyle(1, Math.random() * 0xFFFFFF, 1);
					var x:Number = 250;
					var y:Number = 250;
					canvas.graphics.drawCircle(x, y, 5);
				}
			}
			
			private function redraw2():void
			{
				wait = true;
				t = getTimer();
				// 10,000 circles in random positions
				// 1/4 of a second
				canvas.graphics.clear();
				bitmap.visible = false;
				for (var i:int = 0; i < numIterations.value; i++) {
					canvas.graphics.lineStyle(1, Math.random() * 0xFFFFFF, 1);
					var x:Number = 500 * Math.random();
					var y:Number = 500 * Math.random();
					canvas.graphics.drawCircle(x, y, 5);
				}
			}
			
			private function redraw3():void
			{
				wait = true;
				t = getTimer();
				
				// 10,000 circles using Shape and BitmapData.draw() with Shape
				// 1/2 a second, regardless of position
				
				var shape:Shape = new Shape();
				//shape.graphics.lineStyle(1, 0xFFFFFF, 1);
				var m:Matrix = new Matrix();
				bitmap.bitmapData.lock();
				bitmap.bitmapData.fillRect(bitmap.bitmapData.rect, 0x00000000); // transparent fill
				canvas.graphics.clear();
				bitmap.visible = true;
				for (var i:int = 0; i < numIterations.value; i++) {
					
					var x:Number = 500 * Math.random();
					var y:Number = 500 * Math.random();
					m.identity();
					m.translate(x, y);
					// redraw shape each time
					shape.graphics.clear();
					shape.graphics.lineStyle(1, Math.random() * 0xFFFFFF, 1);
					shape.graphics.drawCircle(0, 0, 5);
					
					bitmap.bitmapData.draw(shape, m);
					//bitmap.bitmapData.draw(shape, m, ct);
				}
				bitmap.bitmapData.unlock();
			}
			
			private var circleBitmapData:BitmapData = new BitmapData(11, 11, true, 0x00000000);
			
			private function redraw4():void
			{
				wait = true;
				t = getTimer();
				
				// 10,000 circles using BitmapData.draw() with BitmapData sprite graphics and ColorTransform
				// 1/4 of a second, regardless of position
				
				var shape:Shape = new Shape();
				shape.graphics.lineStyle(1, 0xFFFFFF);
				shape.graphics.drawCircle(5, 5, 5);
				circleBitmapData.fillRect(circleBitmapData.rect, 0x00000000); // transparent fill
				circleBitmapData.draw(shape);
				
				var ct:ColorTransform = new ColorTransform();
				var m:Matrix = new Matrix();
				bitmap.bitmapData.lock();
				bitmap.bitmapData.fillRect(bitmap.bitmapData.rect, 0x00000000); // transparent fill
				canvas.graphics.clear();
				bitmap.visible = true;
				for (var i:int = 0; i < numIterations.value; i++) {
					
					ct.redMultiplier = Math.random();
					ct.greenMultiplier = Math.random();
					ct.blueMultiplier = Math.random();
					
					var x:Number = 500 * Math.random();
					var y:Number = 500 * Math.random();
					m.identity();
					m.translate(x - 5, y - 5);
					
					bitmap.bitmapData.draw(circleBitmapData, m, ct);
				}
				bitmap.bitmapData.unlock();
			}
			
			private function report():void
			{
				if (wait) // wait 1 frame?
				{
					wait = false;
					return;
				}
				
				if (t != -1)
				{
					trace(title = String(getTimer() - t) + ' ms');
				}
				t = -1;
			}
		]]>
	</mx:Script>
	<ui:ControlPanel>
		<mx:VBox label="First tab">
			<mx:Label text="Example control panel"/>
		</mx:VBox>
		<mx:VBox label="Second tab">
			<mx:Label text="Second tab of example control panel"/>
		</mx:VBox>
	</ui:ControlPanel>
</ui:DraggablePanel>
